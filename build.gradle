plugins {
    id "architectury-plugin"
    id "forgified-fabric-loom" apply false
    id 'maven-publish'
    id("com.github.fudge.forgedflowerloom") apply false
    id("org.jetbrains.kotlin.jvm")
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: "org.jetbrains.kotlin.jvm"

    if (name != "plugin") {
        apply plugin: "forgified-fabric-loom"
        apply plugin: "com.github.fudge.forgedflowerloom"

        loom {
            silentMojangMappingsLicense()
        }
    }


    ext.total_version = "$mod_version+$minecraft_version"
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = targetVersion
        }
    }

    java {
        withSourcesJar()
    }
}

task publishAll {
    dependsOn("fabric:curseforge353890")
    dependsOn("forge:curseforge442354")
}